include "..\\Controllers\\PictureOutputController"
include "..\\Controllers\\MovieOutputController"
include "..\\..\\Classes\\OptionsClass"

/*
  This will identify whether a file is an apparent RAR volume, based on header information.

  This Validator will return the following values:

  -1 = NOT a RAR
   0 = RAR volume, standard
   1 = RAR volume, but length of 22 bytes, meaning it is an empty volume
   2 = RAR volume, but password protected / encrypted - meaning EnCase can't open it.
   3 = UNKNOWN file tag
*/

class RarClass {

  PictureOutputController   MyPictureOutput;
  MovieOutputController     MyMovieOutput;
  String                    Header;
  OptionsClass              MyOptions;

  bool                      HadHeader;

  RarClass(OptionsClass myOptions, PictureOutputController myPictureOutput, MovieOutputController myMovieOutput):
    MyOptions = myOptions,
    MyPictureOutput = myPictureOutput,
    MyMovieOutput = myMovieOutput
  {
    Header = "";
  }

  long Validate(EntryClass entry){
    if(MyOptions.Debug){
      MyPictureOutput.WriteDebug("RARClass", "============================");
      MyPictureOutput.WriteDebug("RARClass", "== ValidateArchive called on " + entry.FullPath());

      MyMovieOutput.WriteDebug("RARClass", "============================");
      MyMovieOutput.WriteDebug("RARClass", "== ValidateArchive called on " + entry.FullPath());
    }

    long             myReturn = -1;
    EntryFileClass   file();

    if(entry.LogicalSize() < 3){
      // must be more than 3 bytes to be a rar...
      if(MyOptions.Debug) {
        MyPictureOutput.WriteDebug("RARClass", "\\/\\/ Logical Size is less than 3.  Process aborted.");
        MyMovieOutput.WriteDebug("RARClass", "\\/\\/ Logical Size is less than 3.  Process aborted.");
      }
      myReturn = -1;

    } else {
      if(MyOptions.Debug){
        MyPictureOutput.WriteDebug("RARClass", "== Opening Entry for examination...");
        MyMovieOutput.WriteDebug("RARClass", "== Opening Entry for examination...");
      }

      file.Open(entry, FileClass::SLACK | FileClass::NOINITIALIZE);
      
      if(file){
        if(MyOptions.Debug) {
          MyPictureOutput.WriteDebug("RARClass", "success");
          MyMovieOutput.WriteDebug("RARClass", "success");
        }

        // Check the first two bytes to validate the header
        String s;
        file.SetCodePage(CodePageClass::ANSI);
        file.ReadString(s, 4);

        HadHeader = false;
        if(s == "Rar!"){
          HadHeader = true;

          // the header matches...
          if(entry.LogicalSize() <= 22){

            // ...but it's an empty file
            if(MyOptions.Debug) {
              MyPictureOutput.WriteDebug("RARClass", "\\/\\/ Valid RAR Header, but too small to hold data.  Process aborted.");
              MyMovieOutput.WriteDebug("RARClass", "\\/\\/ Valid RAR Header, but too small to hold data.  Process aborted.");
            }

            myReturn = 1;

          } else {
            //skip the next 19 bytes
            file.Skip(19);

            //read in next byte
            int i = file.ReadBinaryInt(1, true);

            if((i & 4) == 0) {
              // These values represent acceptable, mountable archives
              if(MyOptions.Debug) {
                MyPictureOutput.WriteDebug("RARClass", "== Valid RAR Header and mount flag");
                MyMovieOutput.WriteDebug("RARClass", "== Valid RAR Header and mount flag");
              }

              myReturn = 0;

            } else if((i & 4) == 4) {
              // These values represent password-protected or otherwise volatile archives
              if(MyOptions.Debug) {
                MyPictureOutput.WriteDebug("RARClass", "== Valid RAR Header, but cannot be mounted by EnCase.");
                MyMovieOutput.WriteDebug("RARClass", "== Valid RAR Header, but cannot be mounted by EnCase.");
              }

              myReturn = 2;

            } else {
              // Anything else is an unknown..
              if(MyOptions.Debug) {
                MyPictureOutput.WriteDebug("RARClass", "== Valid RAR Header, but unknown mount flag value - unsure of action to take.");
                MyMovieOutput.WriteDebug("RARClass", "== Valid RAR Header, but unknown mount flag value - unsure of action to take.");
              }

              myReturn = 3;
            }
          }
        }
      } else {
        if(MyOptions.Debug) {
          MyPictureOutput.WriteDebug("RARClass", "failed!");
          MyMovieOutput.WriteDebug("RARClass", "failed!");
        }
      }
    }

    return myReturn;
  }
}
